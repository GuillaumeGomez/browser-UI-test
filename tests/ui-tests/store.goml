// This test ensures that the `store*` commands are working as expected.
goto: "file://" + |CURRENT_DIR| + "/" + |DOC_PATH| + "/elements.html"
store-value: (background_color, "#111")
// This check should fail.
assert-css: ("header", {"background-color": |background_color|})
store-value: (background_color, "rgb(17, 17, 17)")
// Duplicating variable.
store-value: (background_color2, |background_color|)
assert-css: ("header", {"background-color": |background_color|})
assert-css: ("header", {"background-color": |background_color2|})
store-property: (height, "header", "clientHeight")
// The 4 checks should fail
assert-variable: (height, "12")
assert-variable-false: (height, "24")
assert-variable-false: (height, "2", [STARTS_WITH, CONTAINS])
assert-variable-false: (height, "4", [CONTAINS, ENDS_WITH])
// The 4 checks should succeed
assert-variable: (height, "24")
assert-variable-false: (height, "12")
assert-variable: (height, "2", [STARTS_WITH, CONTAINS])
assert-variable: (height, "4", [CONTAINS, ENDS_WITH])
store-css: (height, "header", "background-color")
// Should succeed
assert-variable: (height, "rgb(17, 17, 17)")
store-attribute: (attr, ".right", "data-whatever")
assert-variable: (attr, "a")
// Should fail
store-attribute: (attr, "header", "data-whatever")
// Variable shouldn't change value.
assert-variable: (attr, "a")
local-storage: {"a key": "a value"}
store-local-storage: (local, "a key")
// Should fail.
assert-variable: (local, "a key")
// Should succeed.
assert-variable: (local, "a value")
store-text: (text, "#another-one")
// Should fail.
assert-variable: (text, "hoy")
// Should succeed.
assert-variable: (text, "hey")
store-value: (css_checks, {"background-color": |background_color|})
assert-css: ("header", |css_checks|)
assert-variable: (css_checks, {"background-color": "rgb(17, 17, 17)"})
// Should fail
assert-variable: (css_checks, "a")
